configfile: "config.yaml"

SAMPLE_NAMES = list(config["samples"].keys())

rule all:
    input:
        "plots/quals.svg"

module mapping:
    snakefile: "sub_workflow/bwa_map.smk"
    config: config

use rule bwa_map        from mapping
use rule samtools_sort  from mapping
use rule samtools_index from mapping

def get_bwa_map_input_fastqs(wildcards):
    return config["samples"][wildcards.sample]

rule bcftools_call:
    input:
        fa="data/genome.fa",
        bam=expand("results/mapped/{sample}.sorted.bam",     sample=SAMPLE_NAMES),
        bai=expand("results/mapped/{sample}.sorted.bam.bai", sample=SAMPLE_NAMES)
    output:
        "calls/all.vcf"
    params:
        rate=config["prior_mutation_rate"]
    log:
        "logs/bcftools_call/all.log"
    shell:
        "(bcftools mpileup -f {input.fa} {input.bam} | "
        "bcftools call -mv -P {params.rate} - > {output}) 2> {log}"

rule plot_quals:
    input:  "calls/all.vcf"
    output: "plots/quals.svg"
    script: "scripts/plot-quals.py"
